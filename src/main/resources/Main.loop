# Sample script in LOOP with minor extensions.

PROGRAM ASSIGN(x1) DO  # return x1;
  x0 := 0
  LOOP x1 DO
    x0 := x0 + 1
  END
END

PROGRAM ADD(x1 ,x2) DO  # return x1 + x2;
  x0 := ASSIGN(x1)
  LOOP x2 DO
    x0 := x0 + 1
  END
END

PROGRAM MULTIPLY(x1 ,x2) DO  # return x1 * x2;
  x0 := 0;
  LOOP x1 DO
    x0 := ADD(x0, x2)
  END
END;

PROGRAM PRED(x1) DO  # return x1 - 1 (0 if x1 <= 1)
  x2 := 0
  LOOP x1 DO
    x0 := ASSIGN(x2);
    x2 := x2 + 1
  END
END;

PROGRAM DIFF(x1, x2) DO  # return x1 - x2 (0 if x1 <= x2)
  x0 := ASSIGN(x1);
  LOOP x2 DO
    x0 := PRED(x0)
  END
END;

PROGRAM SIGNUM(x1) DO  # return (x1 != 0) ? 1 : 0
  x0 := 0;
  LOOP x1 DO
    x0 := 0; x0 := x0 + 1
  END
END

PROGRAM NOT(x1) DO  # return (x1 == 0) ? 1 : 0
  x0 := 0; x0 := x0 + 1;
  LOOP x1 DO
    x0 := 0
  END
END

PROGRAM FACTORIAL(x1) DO  # return x1 ! (factorial)
  x0 := 0 ; x0 := x0 + 1;
  x2 := 0 ; x2 := x2 + 1;
  LOOP x1 DO
    x0 := MULTIPLY(x2, x0);
    x2 := x2 + 1
  END
END

# Simple input and output
PRINT("-------------------------------------------");
PRINT(" Choose 0 <= `x1` <= 11, and `x2` >= `x1`.");
PRINT()
x1 := INPUT("     Value for `x1` : ");
x2 := INPUT("     Value for `x2` : ");
PRINT()
x3 := MULTIPLY(x1, x2);
x4 := DIFF(x2, x1);
x5 := FACTORIAL(x1);
PRINT(" `MULTIPLY(x1, x2)` : ", x3);
PRINT("     `DIFF(x2, x1)` : ", x4);
PRINT("    `FACTORIAL(x1)` : ", x5);
PRINT("-------------------------------------------");
